plugins {
    id 'org.springframework.boot' version '2.4.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'maven-publish'
}

bootJar {
    archiveFileName = 'koderia-flows.jar'
}

group = 'com.obj.nc.flows'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

ext {
    set('testcontainersVersion', "1.15.1")
    set('springCloudVersion', "2020.0.0")
    set('junitJupiterVersion', '5.4.2')
}

dependencies {
    implementation project(':functions')

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-function-context'

    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-rabbit'
    implementation 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    testImplementation("org.springframework.cloud:spring-cloud-stream") {
        artifact {
            name = "spring-cloud-stream"
            extension = "jar"
            type ="test-jar"
            classifier = "test-binder"
        }
    }

    testImplementation 'io.rest-assured:rest-assured:4.2.0'
    testImplementation 'io.rest-assured:json-path:4.2.0'
    testImplementation 'io.rest-assured:xml-path:4.2.0'
    testImplementation 'io.rest-assured:rest-assured-common:4.2.0'
    testImplementation 'io.rest-assured:spring-mock-mvc:4.2.0'
    testImplementation 'io.rest-assured:json-schema-validator:4.1.2'
    testImplementation 'org.json:json:20201115'

    testCompile 'org.junit.jupiter:junit-jupiter-api'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine'
    testCompile 'org.junit.jupiter:junit-jupiter-params'
    testCompile 'org.junit.platform:junit-platform-launcher'
    testCompile 'org.junit.platform:junit-platform-runner'

    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation(testFixtures(project(":functions")))
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.junit:junit-bom:${junitJupiterVersion}"
    }
}

test {
    systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active")
    useJUnitPlatform()
}

task copyDistToStagingFolder(type: Copy) {
    from bootJar // shortcut for bootJar.outputs.files
    into "./../docker/koderia-flows/dist"
}

build.dependsOn copyDistToStagingFolder
